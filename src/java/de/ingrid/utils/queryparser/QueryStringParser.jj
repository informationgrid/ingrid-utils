/**
 * Ingrid Query String Parser
 */

options {
LOOKAHEAD=2;
STATIC=false;
}

PARSER_BEGIN(QueryStringParser)
package de.ingrid.utils.queryparser;

import java.io.StringReader;


import de.ingrid.utils.query.ClauseQuery;
import de.ingrid.utils.query.FieldQuery;
import de.ingrid.utils.query.IngridQuery;
import de.ingrid.utils.query.TermQuery;
import de.ingrid.utils.query.RangeQuery;

public class QueryStringParser {
     /**
     * Parses the string to a query object representation
     * @param queryString
     * @return ingrid query that contains subqueries
     * @throws ParseException
     */
    public static IngridQuery parse(String queryString) throws ParseException {
    	QueryStringParser parser = new QueryStringParser(new StringReader(queryString));
    	return parser.parse();
    }
}

PARSER_END(QueryStringParser)

<*> TOKEN : {
  <#_NUM_CHAR:   ["0"-"9"] >

| <#_ESCAPED_CHAR: "\\" [ "\\", "+", "-", "!", "(", ")", ":", "^",
                          "[", "]", "\"", "{", "}", "~", "*", "?" ] >
| <#_TERM_START_CHAR: ( ~[ " ", "\t", "\n", "\r", "+", "-", "!", "(", ")", ":", "^",
                           "[", "]", "\"", "{", "}", "~", "*", "?" ]
                       | <_ESCAPED_CHAR> ) >
| <#_TERM_CHAR: ( <_TERM_START_CHAR> | <_ESCAPED_CHAR> | "-" | "+" ) >
| <#_WHITESPACE: ( " " | "\t" | "\n" | "\r") >
}

<DEFAULT, RangeIn, RangeEx> SKIP : {
 <<_WHITESPACE>>
}


<DEFAULT> TOKEN : {
  <AND:       ("AND" | "&&" | "+") >
| <OR:        ("OR" | "||") >
| <NOT:       ("NOT" | "!" | "-") >
| <PLUS:      "+" >
| <MINUS:     "-" >
| <LPAREN:    "(" >
| <RPAREN:    ")" >
| <COLON:     ":" >
| <CARAT:     "^" > : Boost
| <QUOTED:     "\"" (~["\""])+ "\"">
| <FLOAT:     ("-")?(<_NUM_CHAR>)+"."(<_NUM_CHAR>)+>
| <TERM:      <_TERM_START_CHAR> (<_TERM_CHAR>)*  >
//| <DATATYPE:      <_TERM_START_CHAR> "datatype" <COLON> (<_TERM_CHAR>)* >
//| <FIELD:      <_TERM_START_CHAR> (<_TERM_CHAR>)* <COLON> (<_TERM_CHAR>)* >
| <FIELD:      <_TERM_START_CHAR> (<_TERM_CHAR>)* <COLON> >
| <FUZZY_SLOP:     "~" ( (<_NUM_CHAR>)+ ( "." (<_NUM_CHAR>)+ )? )? >
| <PREFIXTERM:  <_TERM_START_CHAR> (<_TERM_CHAR>)* "*" >
| <WILDTERM:  <_TERM_START_CHAR>
              (<_TERM_CHAR> | ( [ "*", "?" ] ))* >
| <RANGEIN_START: "[" > : RangeIn
| <RANGEEX_START: "{" > : RangeEx
}

<Boost> TOKEN : {
<NUMBER:    (<_NUM_CHAR>)+ ( "." (<_NUM_CHAR>)+ )? > : DEFAULT
}

<RangeIn> TOKEN : {
<RANGEIN_TO: "TO">
| <RANGEIN_END: "]"> : DEFAULT
| <RANGEIN_QUOTED: "\"" (~["\""])+ "\"">
| <RANGEIN_GOOP: (~[ " ", "]" ])+ >
}

<RangeEx> TOKEN : {
<RANGEEX_TO: "TO">
| <RANGEEX_END: "}"> : DEFAULT
| <RANGEEX_QUOTED: "\"" (~["\""])+ "\"">
| <RANGEEX_GOOP: (~[ " ", "}" ])+ >
}

IngridQuery parse() :
	{ 
		// variables
		Token token;
		IngridQuery query = new IngridQuery();
		IngridQuery subQuery;
//		String dataType;
	}
	{
(
subQuery = term()
	{
		query.addTerm((TermQuery)subQuery);

	}
| subQuery = field()
	{
         query.addField((FieldQuery)subQuery); 
    }
| subQuery = range()
	{
         query.addRangeQuery((RangeQuery)subQuery); 
	}
| subQuery = clause()
	{ 
		query.addClause((ClauseQuery)subQuery);
	}	

)*
	{return  query;}
}


	
// field
	IngridQuery field() :
	{
		FieldQuery query;
		Token key;
		Token value;
		boolean required = true;	
		boolean prohibited = false;
	}
	{
	(<OR>{required = false; } | <NOT> {prohibited =true; }| <AND> {required =true; })?
	key=<FIELD>
	(value=<FLOAT>|value=<TERM>|value=<PREFIXTERM>|value=<QUOTED>)
	{query = new FieldQuery( required, prohibited,  key.image.substring(0, key.image.length() -1).toLowerCase(), value.image);}
	{return query;}
	}
	
	
// term	
	IngridQuery term() :
	{
		TermQuery query;
		Token t;
		boolean required = true;	
		boolean prohibited = false;
	}
	{
	(<OR>{required = false; } | <NOT> {prohibited =true; }| <AND> {required =true; })?
	(t=<TERM>|t=<PREFIXTERM>|t=<QUOTED>)
	{query = new TermQuery(required,prohibited, t.image);}
	{return query;}
	}
	
// range queries

	IngridQuery range() :
	{
		RangeQuery query;
		Token key;
		Token goop1;
		Token goop2;

		boolean in;
		boolean required = true;	
		boolean prohibited = false;
	}
	
	{
	(<OR>{required = false; } | <NOT> {prohibited =true; }| <AND> {required =true; })?
	key=<FIELD>
	<RANGEIN_START> ( goop1=<RANGEIN_GOOP>|goop1=<RANGEIN_QUOTED> )
    [ <RANGEIN_TO> ] ( goop2=<RANGEIN_GOOP>|goop2=<RANGEIN_QUOTED> )
    <RANGEIN_END> 
    	
	// angeQuery(boolean required, boolean prohibited, String fieldName,  String from, String to, boolean inclusive)
	{query =new RangeQuery(required,prohibited, key.image.substring(0, key.image.length() -1).toLowerCase(), goop1.image, goop2.image, true );}
	{return query;}
	}
	

// clause...
	IngridQuery clause() :
	{
		
		ClauseQuery query;
		Token t;
		boolean required = true;	
		boolean prohibited = false;
		IngridQuery subQuery;
	}
	{
	(<OR>{required = false; } | <NOT> {prohibited =true; }| <AND> {required =true; })?
	{query =  new ClauseQuery( required, prohibited );}
	<LPAREN>
	 (
	subQuery = term()
		{
			query.addTerm((TermQuery)subQuery);
		}
	| subQuery = field()
		{
			query.addField((FieldQuery)subQuery);
		}
	| subQuery = clause()
		{ 
			query.addClause((ClauseQuery)subQuery);
		}	
	
	)*
	 <RPAREN>
	
	{return  query;}
	}	
	
